################################################################################
# Copyright (C) 2012
# Institute for System Programming, Russian Academy of Sciences (ISPRAS).
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
################################################################################

use Env qw(LDV_DEBUG LDV_CPACHECKER_ET_CONV_DEBUG);

# Get from a string given a node location (file and line) and a node value.
# args: a node string, flag says whether a source reference should be updated.
# retn: file, line, value.
sub get_loc_and_val($$);

# We'd like to relate a given CPAchecker error trace with original source code,
# not with the intermediate (CIL) one. So first of all build a map between
# sources.
my %src_map;

my @intermediate_source_file;

# Read an intermediate source file from the beginning of the error trace.
unless ($opts->{'fh intermediate source file'})
{
  my $header = shift(@et_array);
  if (defined($header))
  {
    if ($header =~ /^-------(.*)-------$/)
    {
      print_debug_trace("Process '$1' intermediate source file");

      while (scalar(@et_array))
      {
        my $str = shift(@et_array);

        last if ($str eq "--------------\n");

        push(@intermediate_source_file, $str);
      }

      print_debug_warning("An intermediate source file isn't terminated with"
        . " '--------------'");
    }
    else
    {
      print_debug_warning("An intermediate source file isn't specified for a"
        . " CPAchecker error trace processed. A first line is '$header'");
      # Return the first line of an error trace processed back.
      unshift(@et_array, $header);
    }
  }
}
else
{
  my $fh = $opts->{'fh intermediate source file'};
  @intermediate_source_file = <$fh>;
}

my $src_cur = '';
my $src_line_cur = 0;
my $cil_line_cur = 1;
while (scalar(@intermediate_source_file))
{
  my $str = shift(@intermediate_source_file);

  chomp($str);

  if ($str =~ /^#line (\d+) "([^"]+)"$/)
  {
    $src_line_cur = $1;
    $src_cur = $2;
  }
  elsif ($str =~ /^#line (\d+)$/)
  {
    $src_line_cur = $1;
  }
  else
  {
    $src_map{$cil_line_cur} = {
        'file' => $src_cur
      , 'line' => $src_line_cur};
    $src_line_cur++;
  }

  $cil_line_cur++;
}

# Read the whole error trace specified.
my @et_raw = @et_array;

# Add such the header to distinguish error traces in the common format.
push(@et_conv_array, "Error trace common format v0.1\n");

# Print a path to source code file just one time if it isn't changed.
my $src_prev = '';
# To extract initialization section.
my $isinit = 0;

# Delete all not supported lines. This has meaning for error traces of new
# CPAchecker releases (~1.2).

# Find indexes for not supported lines.
my @del_indexes = reverse(grep { $et_raw[$_] =~ /Real:/ } 0..$#et_raw);

# Delete lines by their indexes.
foreach my $index (@del_indexes)
{
  splice(@et_raw, $index, 1);
}

# Convert a raw error trace into the common format line by line.
while (@et_raw)
{
  my $str = shift(@et_raw);
  chomp($str);
  next if ($str =~ /^\s*$/);

  my ($src, $line, $val) = get_loc_and_val($str, 1);

  # Do not treat empty nodes.
  next if ($val =~ /^\s*$/);

  my $type = '';
  my $kind = '';
  my $skip_reason = '';
  my $text = '';

  # Begin an artificial initialization function call.
  if ($val eq 'INIT GLOBAL VARS')
  {
    $type = 'CALL';
    $kind = 'INIT';
    $text = ': __CPAchecker_initialize()';
    $isinit = 1;
  }
  elsif ($val eq 'Function start dummy edge')
  {
    # Begin an entry point that finishes a current initialization function call.
    if ($isinit)
    {
      push(@et_conv_array, join(' ', (0, $src, 'RETURN', '', '')));
      $type = 'CALL';
      $kind = 'ENTRY';
      $isinit = 0;
    }
  }
  elsif ($val =~ /^\[(.*)\]$/)
  {
    $type = 'ASSUME';
    $text = ": $1";
  }
  # Function call is either 'func(...);?' or 'var = func(...);'.
  elsif ($val =~ /^([_A-Za-z][_A-Za-z0-9]*\(.*\));?$/
    or $val =~ /^(.* = [_A-Za-z][_A-Za-z0-9]*\(.*\));$/)
  {
    $type = 'CALL';
    my $call = $1;

    # See next line to understand whether a given function call is skipped.
    if (my $str_next = shift(@et_raw))
    {
      my ($src_next, $line_next, $val_next) = get_loc_and_val($str_next, 0);

      if ($val_next eq 'Function start dummy edge')
      {
        # Just ignore this string at all.
      }
      else
      {
        $skip_reason = 'SKIP("Function call is skipped due to function is undefined")';
        # Return the next string back.
        unshift(@et_raw, $str_next);
      }
    }

    $text = ": $call";
  }
  # Block is everything that contains ' = ' and finishes with ';' except
  # function calls that contains an assignment.
  elsif ($val =~ /^(.*) = (.*);$/)
  {
    $type = 'BLOCK';
    $text = ": $1 = $2;";
  }
  elsif ($val =~ /^return\s*(.*);$/)
  {
    $type = 'RETURN';
    if ($1)
    {
      # Remove braces.
      $1 =~ /^\((.*)\)$/;
      $text = ": $1";
    }
    else
    {
      $text = ": ";
    }

    # See next line to remove remove edge.
    if (my $str_next = shift(@et_raw))
    {
      my ($src_next, $line_next, $val_next) = get_loc_and_val($str_next, 0);

      if ($val_next =~ /^Return edge/)
      {
        # Just ignore this string at all.
      }
      else
      {
        # Return the next string back.
        unshift(@et_raw, $str_next);
      }
    }
  }
  # Declaration is everything that ends with ';' except function calls, blocks
  # and returns.
  elsif ($val =~ /^(.*);$/)
  {
    $type = 'DECLARATION';
    $text = ": $1";
  }
  # Treat labels, gotos and whiles as usual blocks.
  elsif ($val =~ /^Label: (.*)$/)
  {
    $type = 'BLOCK';
    $text = ": $1:";
  }
  elsif ($val =~ /^Goto: (.*)$/)
  {
    $type = 'BLOCK';
    $text = ": goto $1;";
  }
  elsif ($val =~ /^while$/)
  {
    $type = 'BLOCK';
    $text = ": while;";
  }
  else
  {
    print_debug_warning("CPAchecker node value '$val' wasn't recognized");
    $text = ": $val";
  }

  push(@et_conv_array
    , join(' ', ($line, $src, $type, $kind, $skip_reason, $text)));
}

{
# Supress warnings like 'Subroutine get_loc_and_val redefined' in the manner
# recommended at http://perldoc.perl.org/perldiag.html.
no warnings 'redefine';

sub get_loc_and_val($$)
{
  my $str = shift;
  my $issrc_prev_update = shift;

  # At the beginning a node value equals to a node string.
  my $val = $str;

  # Try to get a corresponding line number.
  my $line = '';
  if ($val =~ /^\s*Line\s*([^:]+):/)
  {
    $val = $POSTMATCH;
    $line = $1 if (defined($1));
  }

  # Elimanate "brackets" from the rest part of a string.
  if ($val =~ /^\s*N\d+\s*-\s*{\s*(.*)\s*}\s*-\s*>\s*N\d+$/)
  {
    $val = $1;
  }
  else
  {
    print_debug_warning("A CPAchecker error trace specified has unsupported"
     . " format in string '$str'");
    # So don't process this string further.
    return ('', $line, '');
  }

  # Do not get a source code reference for empty nodes.
  return ('', $line, $val) if ($val =~ /^\s*$/);

  # Try to get a corresponding source code location.
  my $src = '';
  if ($src_map{$line})
  {
    # Use a corresponding path just in case when it exists and differs from the
    # previous one.
    my $file = $src_map{$line}{'file'};
    if ($file ne $src_prev)
    {
      $src = '"' . $file . '"';
      $src_prev = $file if ($issrc_prev_update);
    }

    # Also fix up a line number.
    $line = $src_map{$line}{'line'};
  }
  # Do not treat zero line.
  elsif ($line)
  {
    print_debug_trace("Can't find a corresponding source code for line"
      . " '$line'");
  }

  return ($src, $line, $val);
}

}
