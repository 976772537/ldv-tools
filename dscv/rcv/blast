#!/usr/bin/perl

# RCV frontend for BLAST tool.

# For elaborate description of writing the verifiers, please, refer to CPAchecker's wrapper located in dscv/rcv/cpachecker.
#
# In this file you may see examples of how to use $hints, and how to attach additional files to the report.  You may also see more automata examples here.

set_tool_name("BLAST");

# locate blast
my $blast_bin_dir = $ENV{'BLAST_PATH'} || "$installed_backends_dir/blast/bin";
my $blast_exe = "$blast_bin_dir/pblast.opt";

-d $blast_bin_dir or die "The selected BLAST's backend directory '$blast_bin_dir' is not a directory";

# Add path for BLAST's supplementary scripts.  Prepending to system's to override other PATHs.
my $new_path = "$blast_bin_dir:".$ENV{'PATH'};
vsay 30,"Setting PATH to '$new_path'";
$ENV{'PATH'} = $new_path;
vsay 40,"CSIsat to be used is ".`which csisat`;

my @files = preprocess_all_files('cpp');

# Options that will be passed to the blast tool
my @blast_options = ($blast_exe,@files);

# Set default options
# These are: -predH 7 -lattice -include-lattice symb -include-lattice list -craig 2
push @blast_options,
	"-predH","7","-craig","2",	# Default heuristics on
	"-ignoredupfn", # Ignore duplicated functions when constructing CFA
	"-nosserr", # Ignore "combining structure and non-structure values" error.  LLVM emits such nasty code.
	"-skipnorm",# Do not try to normalize predicates within BLAST.  Solvers do it much faster.
	"-enable-recursion",# Allow recursive call graphs, but cut out the actual recursive calls
;

# Check the number of entry points 
die "You can only specify one main for BLAST!" if (scalar @mains != 1);

# Set the option for the entry point
my $main = $mains[0];
push @blast_options, "-main",$main;

# Check the number of error locations
die "You can only specify one error label for BLAST!" if (scalar @errlocs != 1);
my $errloc = $errlocs[0];
push @blast_options, "-L",$errloc;

# Interpret hints
# Cldepth
if (my $cld = $hints_tag->first_child_text('cldepth')){
	push @blast_options,"-cldepth",$cld;
}else{
	# Temporarly set cldepth to zero
	push @blast_options,"-cldepth","0";
}
# Aliases may be explicitely required by the model
my $do_aliases = $hints_tag->first_child_text('alias');

# We may also activate the aliases via env var.
$do_aliases ||= $ENV{'BLAST_ALIASES'};
if ($do_aliases){
	vsay ('NORMAL',"Running BLAST with alias analysis (this may take longer).\n");
	push @blast_options,
		"-alias","bdd",	# Turn alias analysis on
		"-cref",	#"Complete REFinement", performs alias iterations, implies alias analysis
		"-nomusts","-const",	#Aliasing heuristics implemented in ISPRAS
	;
}else{
	vsay ('DEBUG',"Running BLAST without alias analysis.\n");
	push @blast_options, "-alias","";	# Turn alias analysis off
}

# Show a warning if an obsolete variable to trigger alias analysis is used
vsay('WARNING',"Use of BLAST_EXP_NOALIAS is obsolete!!\n") if $ENV{'BLAST_EXP_NOALIAS'};

# Filter also lattices if specified
unless ($ENV{'BLAST_EXP_NOLATTICE'}){
	push @blast_options,
		"-lattice","-include-lattice","symb", 	# Symbolic-store lattice
	;
}

# Filter stopsep
unless ($ENV{'BLAST_NOSTOPSEP'}){
	push @blast_options, "-stop-sep"; # Use stop-sep instead of stop-join
	unless ($ENV{'BLAST_MERGE'}){
		push @blast_options, "-merge","bdd"; 	# Merge at equiv bdds, default
	} else {
		push @blast_options, "-merge", $ENV{'BLAST_MERGE'};
	}
}

# Rerouting functionality enabling
my $rerouteT = $hints_tag->first_child('rerouter');
if ($rerouteT){
	my @reroute_map = $rerouteT->children_text('map');
	my @reroute_map2 = $rerouteT->children_text('map2');
	my @reroute_foreach = $rerouteT->children_text('foreach');
	my $placeholder = $rerouteT->first_child_text('placeholder');
	# Skip if there's not map and foreach handlers
	if (@reroute_map || @reroute_foreach){
		push @blast_options,"-reroute";
		if($rerouteT->first_child('first_only')) {
			push @blast_options,"-reroute-first";
		}
		local $_;
		push @blast_options,"-reroute-map",$_ for @reroute_map;
		push @blast_options,"-reroute-map2",$_ for @reroute_map2;
		push @blast_options,"-reroute-foreach",$_ for @reroute_foreach;
		push @blast_options,"-reroute-placeholder",$placeholder if $placeholder;
	}
}

# External settings
push @blast_options,(split /\s+/,$ENV{'BLAST_OPTIONS'});
# If an SMT solver is specified via environment, make sure that BLAST uses SMTlib solvers
push @blast_options,"-smt" if $ENV{'BLAST_SOLVER'};

# Use BLAST's ocamltune script to tune OCaml's performance
unless ($ENV{'BLAST_NOOCAMLTUNE'}){
	unshift @blast_options, "ocamltune";
	vsay ('DEBUG',sprintf("OCaml machine-specific tuning is performed!"));
}else{
	vsay ('NORMAL',sprintf("OCaml machine-specific tuning is not performed."));
}

add_time_watch(
	'PBLAST.OPT' => '.*pblast\.opt.*',
	'SMT_SOLVER' => 'CHILD:.*smtlibServer.*',
);

# Fork and run BLAST in a child process.	Redirect its output (std and err) to a file

add_automaton(safe_unsafe());

my $error_trace = "$tmpdir/error_trace.txt";
# It's important to specify 'stdout' here, as the output to the stderr may clobber the trace and make the Error Trace Visualizer fail, yielding a "Script error" for this driver.
add_automaton(dumptrace($error_trace),'stdout');

add_automaton(ocaml_detect());

# Dump the last 50 lines from stderr output of the tool
add_automaton(tail_automaton(50),'stderr');

my ($result, $errcode, $automata_results, $debug_trace) = run(@blast_options);

# BLAST prints the verdict into the standard output.  It was read by the relevant automaton.
my $verdict = $automata_results->{VERDICT} || 'unknown';

# You may skip specifying the description; however if there's something wrong, and you want to discriminate the issues in your tool in the statistics visualizer (or to gather the results for a scientific experiment), you might consider adding something here
my $description = '';

if ($errcode == 5){
	# BLAST didn't find input file -- integration error
	$description = "BLAST couldn't find some input files";
	$verdict = 'unknown';
}elsif($errcode == 3){
	vsay ('WARNING', "BLAST couldn't find entry point/error location!");
	$description = 'No entry point or error location found';
}elsif($errcode == 0 && $result eq 'OK'){
	vsay (20,"Finished.\n");
}else{
	vsay (20,"Failed with exit code $errcode by $result.  But who cares?\n");
}

$description .= $automata_results->{'EXCEPTION'}."\n" if $automata_results->{'EXCEPTION'};

# Add info on OCAML errors
$description.=$automata_results->{'OCAML'}."\n" if $automata_results->{'OCAML'};

# Add the stderr tail
$description.= join ("\n",@{$automata_results->{'TAIL'}})."\n" if $automata_results->{'TAIL'};



# Describe the attributes of the current launch
$description .= "\n=========== BLAST attrubutes ===========\n";

# Add a stopsep information to description
my $stopsep = "default stop-join"; 
unless ($ENV{'BLAST_NOSTOPSEP'}) {
	if($ENV{'BLAST_MERGE'}) {
		$stopsep = "stop-sep with merge at $ENV{'BLAST_MERGE'}";
	} else {
		$stopsep = "stop-sep with default merge";
	}
}
$description .= "$stopsep\n";

#Add aliases info
$description .= ($ENV{'BLAST_EXP_NOLATTICE'} ? "No lattice\n" : '').($ENV{'BLAST_ALIASES'} ? "With aliases\n" : '');

# Convert obtained error trace to the common format. Also keep the original one.
my $error_trace_original = $error_trace;
my $error_trace_common = $error_trace_original . '.common';
my @et_conv = ("$installed_frontends_dir/blast-et-conv.pl",
	"--report=$error_trace",
	"--report-out=$error_trace_common");
local $"=" ";
vsay('INFO', "Calling BLAST error trace converter: @et_conv\n");
system(@et_conv);
die("BLAST error trace converter (@et_conv) failed\n")
	if (check_system_call());
# From now error trace is in the common format.
$error_trace = $error_trace_common;

result(
	# verdict and description should be specified; an error trace is also required if the verdict is "unsafe"
	verdict => $verdict,
	description => $description,
	error_trace => $error_trace,
	# Additional files that are important
	verifier_out => $debug_trace,
	error_trace_original => $error_trace_original,
);

#===================
#  AUTOMATA
#===================

# Verdict check automaton
sub safe_unsafe
{
	return sub {
		my $l=shift or return undef;
		$l=~/The system is safe/ and return {VERDICT=>'safe'};
		$l=~/The system is unsafe/ and return {VERDICT=>'unsafe'};
		return undef;
	};
}

sub exception
{
	return sub {
		my $l=shift or return undef;
		$l=~/Ack! The gremlins again!: (.*)/ and return {'EXCEPTION' => "Exception: $1"};
		return undef;
	};
}


# Trace dumper.  Usage:  my $dumper = dumptrace(filename);
use constant { TRACE_WAIT => 0, TRACE_READ => 1, TRACE_DONE =>2};
sub dumptrace
{
	my $fname = shift or die;
	my $state = TRACE_WAIT;
	my $current_fh;
	return sub {
		my $line = shift;

		!defined $line && $state==TRACE_DONE and do{ close $current_fh; return undef;};
		return undef if $state == TRACE_DONE;

		$line =~ /Error trace: \(final\)/ and do {$state = TRACE_READ; return undef;};
		$line =~ /Error trace ends \(final\)/ and do {$state = TRACE_DONE; return undef;};

		if ($state == TRACE_READ){
			unless ($current_fh){
				# Do not open for appending, open for writing instead (see bug #1264)
				open $current_fh, ">", $fname or die;
			}
			print $current_fh $line;
		}
		return undef;
	};
}

# OCaml error detector
sub ocaml_detect
{
	return sub {
		my $line = shift;
		!defined $line and return undef;

		if ($line =~ /(Fatal error: out of memory.)/){
			return {'OCAML'=>"OCaml exception: $1"};
		}

		return undef;
	};
}

__END__

TODO:
- OCaml exceptions
- More detailed description

# Post limits and verdict on different lines!
$result->{'BLAST-detailed'} = join "\n",(grep {$_} ($result->{'BLAST-detailed'},$result->{'LIMITS'},$result->{'OCAML'}));

$result->{'BLAST-detailed'} .= <<EOR ;

=========== Launch information ===========
Verifier: BLAST ($ver4rep)
Time Limit: $tl4rep
Memory Limit: $ml4rep
Additional options: $ENV{'BLAST_OPTIONS'}
Solver: @{[ $ENV{'BLAST_SOLVER'} || 'default' ]}
@{[$ENV{'BLAST_EXP_NOLATTICE'} ? 'No lattice\n' : '']}@{[$ENV{'BLAST_ALIASES'} ? 'With aliases\n' : '']}
Using $stopsep
=========== Launch inform ends ===========
EOR

