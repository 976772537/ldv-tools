#!/usr/bin/perl

################################################################################
# Copyright 2010-2012
# Institute for System Programming, Russian Academy of Sciences (ISPRAS).
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
################################################################################

#=============================================
=head1 NAME

dscv - verify C program against one or several properties from a property database.

DSCV stands for B<Domain-Specific C Verifier>.  This is a working title.

=head1 SYNOPSIS

Compilation commands must be passed to the tool (all at once or sequentially), as well as a list of properties, and working directory. 

DSCV supports both cmdline interface (when a file with build commands is supplied) and direct interface (when files to verify are directly passed on command line).

If there's no argument named "inline", C<dscv> runs in cmdfile mode:

	dscv --cmdfile=commands.xml --rule-models=0032a,0039

Otherwise, DSCV runs in an inline mode:

	dscv --rcv-verifier=blast inline options -I include/ files file1.c file2.c

=head1 DESCRIPTION

TODO

=cut

use strict;

=head1 COMMAND LINE OPTIONS

=over 8

=cut

use Pod::Usage;
use Getopt::Long;

# Configuration structure
my $config={
	rawcmdfile=>'',
	dir=>'./',
};

my %optionSpecs = ();

=item B<--cmdfile>

File with build commands.  The format is standard for LDV project.  Must be specified, at the current state of art.

=cut
$optionSpecs{'cmdfile=s'} = \$config->{cmdfile};

=item B<--rule-model=model_id>, B<--rule-models=model_id,model_id,...>

Properties to check.  They are looked for in property datbase.

=cut
$config->{property} = [];
$config->{properties} = [];
$optionSpecs{'rule-model=s@'} = \$config->{property};
$optionSpecs{'rule-models=s@'} = \$config->{properties};
$optionSpecs{'rawcmdfile=s'} = \$config->{rawcmdfile};

=item B<--report-out>=report.xml

Outfile to print comprehensive report about all rules to.

=cut
$optionSpecs{'report-out=s'} = \$config->{report};

=item B<--rcv-verifier=backend>

Overrides the reachability C verifier passed by rule instrumentor.

=cut
$config->{verifier_override} = {} unless exists $config->{verifier_override};
$optionSpecs{'rcv-verifier=s'} = \$config->{verifier_override}->{rcv};

=item B<--no-aspect>

B<ONLY inline mode!>  Turns off aspectator.  Ignored if some rule models are specified.

=cut
$config->{inline_aspect} = 1;
$optionSpecs{'aspect!'} = \$config->{inline_aspect};

=item B<--force>

Runs all the toolset even if no entry points were specified in the input cmdfile.  By default BLAST skips all checks and doesn't run Rule Instrumentor and RCV in such case.

=cut
$config->{force} = '';
$optionSpecs{'force'} = \$config->{force};


=back

=cut
my %inline_handlers = ();
sub is_handler_keyword
{
	my $x = shift;
	local $_;
	# undef is thought of as a keyword (since it terminates the last sequence)
	return (!defined $x) || (grep {$_ eq $x} keys %inline_handlers);
}
# Create subroutine that, if at the beginning of input array there is a keyword, reads keyword-related words into $$target array; up to the next keyword otto the end of the array
sub make_handler
{
	my ($keyword, $target) = @_;
	$$target ||= [];
	return sub{
		return () unless @_;
		return @_ if $_[0] ne $keyword;
		shift;	#remove keyword
		my $f = shift;
		while (!is_handler_keyword($f)){
			push @$$target, $f;
			$f = shift;
		}
		return () unless defined $f;
		return ($f,@_);
	};
};


=pod
Inline mode options

=over 8

=item B<files> [list of files]

In inline mode, specifies the list of files that will be verified as if they were linked together.

=cut
$inline_handlers{'files'}=make_handler('files',\$config->{inl_files});

=item B<options> [list of GCC options]

In inline mode, specifies the list of GCC options that will be passsed to compiler and/or preprocessor

=cut
$inline_handlers{'options'}=make_handler('options',\$config->{inl_options});
=item B<option> [list of GCC options]

In inline mode, specifies the list of entry points to check.  If omitted, only one entry point, C<main>, is checked.

=cut
$inline_handlers{'entries'}=make_handler('entries',\$config->{inl_entries});

=back

=head1 ENVIRONMENT VARIABLES

=over 8

=cut

=item B<LDV_RULE_DB>

Rule database address.  It is interpreted by rule interpreter.  Also, the rule database should contain installing script, C<install> that prepares an instance of rule database for Rule Instrumentator to work.

=cut
$config->{rule_db_dir} = $ENV{'LDV_RULE_DB'};

=item B<WORK_DIR>

Working directory for a current instance of LDV scripts.  Unless specified, a current working dir is taken as working.

=cut
use Cwd;
$config->{work_dir} = $ENV{'WORK_DIR'};

=item B<DSCV_HOME>

Base directory, into which DSCV-related scripts and backends are installed.  Set automatically, but may be overridden.

=cut
use FindBin;
BEGIN {
	$ENV{'DSCV_HOME'} ||= "$FindBin::Bin/..";
	$ENV{'LDV_HOME'} ||= "$FindBin::Bin/..";
}
$config->{dscv_dir} = "$ENV{'DSCV_HOME'}/dscv";

BEGIN {
	# Add the lib directory to the @INC to be able to include local modules.
	push @INC,"$ENV{'LDV_HOME'}/shared/perl";
}

=item B<LDV_DEBUG>, B<DSCV_DEBUG>

Level of verbosity, ranging from 0 (silent) to 100 (trace).  Default is 10.  B<DSCV_DEBUG> takes precedence over B<LDV_DEBUG>.

=cut
$config->{verbosity} = $ENV{'DSCV_DEBUG'} || $ENV{'LDV_DEBUG'};
$config->{verbosity} = 10 unless defined $config->{verbosity};

=item B<RCV_VERIFIER>

Reachability C verification tool used to checking all models, regardless of what is specified by Rule-instrumentor.  May be overridden by C<--verifier> option.

=cut
$config->{verifier_override}->{rcv} = $ENV{'RCV_VERIFIER'};


=item B<RULE_INSTRUMENTOR>

Specify custom rule instrumentor.  Unless specified, the program is deduced from C<DSCV_HOME> environment or taken from path.

=cut
$config->{rule_ins} = $ENV{'RULE_INSTRUMENTOR'} || "$ENV{'LDV_HOME'}/bin/rule-instrumentor.pl" || 'rule-instrumentor.pl';

=item B<RI_CACHE>

Cache directory for Rule Instrumentor.  When compiling large projects with many repeatedly checked files (if they're re-used among several verification tasks), we might want to cache these files after they're preprocessed with rule instrumentor.  If you specify folder in this variable, it will be used as cache.

To set different modes, use C<RI_CACHE_MODE> env variable.  Currently, only default and "git" modes are supported.

=cut
$config->{ri_cache} = $ENV{'RI_CACHE'};
$config->{ri_cache_mode} = $ENV{'RI_CACHE_MODE'};


=item B<VERIFIER_OVERRIDE>

Overrides the verifier returned by the Rule Instrumentor.  C<--verifier> option takes precedence.

=cut
$config->{verifier_override_env} = $ENV{'VERIFIER_OVERRIDE'};


=back

=cut
$config->{verifier_override_env} = $ENV{'VERIFIER_OVERRIDE'};

$config->{timestats} = $ENV{'TIMESTATS'} || "$ENV{'LDV_HOME'}/shared/sh/timeout" || 'timeout';

sub usage
{
	my $msg=shift;
	return pod2usage(-msg => $msg, -verbose => 99, -sections=>'SYNOPSIS|COMMAND LINE OPTIONS|ENVIRONMENT VARIABLES');
}

# Determine mode
my $mode;
my @inline_args = ();
$config->{cmdfile} and $mode = 'cmd';
$config->{rawcmdfile} and $mode = 'raw';
if (grep {m/^inline$/} @ARGV){
	$mode = 'inline';
	# Cut last portion of ARGV into a separate array
	my ($index) = grep { $ARGV[$_] eq 'inline' } 0..$#ARGV;
	@inline_args = @ARGV[($index+1)..$#ARGV];
	@ARGV = @ARGV[0..$index-1];
} elsif( grep {m/rawcmdfile/} @ARGV) {
	$mode = 'raw';
}else{
	$mode = 'cmdfile';
}

GetOptions(%optionSpecs) or usage("Unrecognized options!");

use LDV::Utils;
LDV::Utils::set_verbosity($config->{verbosity});
LDV::Utils::push_instrument('dscv');


if($mode eq 'raw') {
	-f $config->{'rawcmdfile'} or die "Command file for raw mode '$config->{rawcmdfile}' does not exists: $!";
	my %args = ();

	%args = Utils::xml_to_hash($config->{rawcmdfile},{to_array => [qw(files errlocs)], to_xml => [qw(hints)]});

	# in this mode work dir is passed as ENV.WORK_DIR
	#               and rule model db as ENV.RULE_MODEL_DB
	$config->{work_dir} = $args{workdir};
	$config->{rule_db_dir} = $args{ruledb};
	-d $config->{work_dir} or die "the workdir '$config->{work_dir}' is not a directory!";
	-f $config->{rule_db_dir} or do{
		vsay ('WARNING', $config->{rule_db_dir}." is not a good Rule DB.  Falling back to env. var.\n");
		$config->{rule_db_dir} = $ENV{'LDV_RULE_DB'};
		-f $config->{rule_db_dir};
	} or die "the rule_db_dir '$config->{rule_db_dir}' is not a file!";
	# Setting work_dir is important for Rule Instrumentor
	$ENV{'WORK_DIR'} = $config->{work_dir};

	#read other options
	$config->{report} = $args{report};
	$config->{properties} = [$args{properties}];
	$config->{cmdfile} = $args{cmdfile};
	-f $config->{cmdfile} or die "the cmdfile '$config->{cmdfile}' is not a file!";
	$mode = 'cmdfile';
}

# Do options tweaking (that doesn't require any semantical checking)

# Compact rule models
$config->{properties} = [split (/,/,join(',',(@{$config->{property}},@{$config->{properties}})))];

# INLINE mode preparation

use File::Copy;
use File::Path qw(mkpath rmtree);
use File::Spec;
# If we're in inline mode, we should create appropriate cmdfile in the working dir and use it as such.
if ($mode eq 'inline'){
	# First we parse options
	my @args = @inline_args;
	while (@args){
		my $len = scalar @args;
		@args = $_->(@args) for values %inline_handlers;
		# If we haven't processed anything, then something's wrong here.
		usage("Specify inline mode modifier at the beginning please") if $len eq scalar @args;
	}
	# Then we generate workdir
	$config->{work_dir} = "verify-".join('-',@{$config->{inl_files}});
	$config->{work_dir} = File::Spec->rel2abs($config->{work_dir});
	$ENV{'WORK_DIR'} = $config->{work_dir};
	rmtree($config->{work_dir});

	# Allocate file for "commands"
	my $CMDFILE;
	mkpath($config->{work_dir});
	my $cmdfile_name = "$config->{work_dir}/generated_cmdfile.xml";
	open $CMDFILE, ">", $cmdfile_name or die;
	my $streamT = XML::Twig::Elt->new('cmdstream');
	my $id = 1;	#ID with which entities are tagged
	my @outfiles = ();	# dummy o-files that will be created for input files.

	# Check if we verify default property (assert()).  If it's the case, we must include a special header file and tweak properties array.
	my $defprop = !@{$config->{properties}};

	# Create basic XML entry for compilation of each C file.
	for my $file (@{$config->{inl_files}}){
		my $ccT = XML::Twig::Elt->new('cc',{id=>$id++});
		# Add option tags
		XML::Twig::Elt->new('opt',{},$_)->paste(last_child => $ccT) for @{$config->{inl_options}};

		# Add input-output tags
		XML::Twig::Elt->new('in',{},$file)->paste($ccT);
		my $outfile = $file; $outfile =~ s/(.*)\.(.*)$/$1.o/;
		XML::Twig::Elt->new('out',{},$outfile)->paste($ccT);
		XML::Twig::Elt->new('cwd',{},getcwd())->paste($ccT);

		push @outfiles, $outfile;

		# Add default-property header
		if ($defprop){
			XML::Twig::Elt->new('opt',{},'-include')->paste(last_child => $ccT);
			XML::Twig::Elt->new('opt',{},'engine-blast.h')->paste(last_child => $ccT);
		}

		$ccT->paste(last_child => $streamT);
	}

	# Create a dummy "link" command.
	my $ldT = XML::Twig::Elt->new('ld',{id=>$id++});
	XML::Twig::Elt->new('in',{},$_)->paste($ldT) for @outfiles;
	XML::Twig::Elt->new('out',{check=>'true'},'link.exe')->paste($ldT);
	XML::Twig::Elt->new('cwd',{},getcwd())->paste($ldT);
	push @{$config->{inl_entries}},'main' unless @{$config->{inl_entries}};
	XML::Twig::Elt->new('main',{},$_)->paste($ldT) for @{$config->{inl_entries}};
	$ldT->paste(last_child=>$streamT);

	# Paste basedir
	XML::Twig::Elt->new('basedir',{},"./")->paste($streamT);

	# Write XML to the file and close it
        $streamT->set_pretty_print('indented');
        $streamT->print($CMDFILE);
	close $CMDFILE or die;

	#Prepare usual DSCV options and proceed to the rest of the script
	$config->{cmdfile}=$cmdfile_name;
	if ($defprop){
		my $default_rule_model = $config->{inline_aspect}?'NONE':'BZ_2';
		$config->{properties}=[$default_rule_model];
	}

	# Place report for easier user access
	$config->{report} = "$config->{work_dir}/report.xml";
}
 
$config->{cmdfile} or usage("Work without cmdfile is not supported!\n");
$config->{work_dir} or usage("WORK_DIR is not specified!\n");
unless (scalar @{$config->{properties}}){
	usage("You should specify at least one property to verify!  Did you forget about it?\n");
}

# Prepare working dir
mkpath ($config->{work_dir});


# Get "key" from LDV watcher (the guy who queues processes).
# If this process is not watched for (i.e. watcher was not started, and it's a user's action that initiated the verification--i.e. inline mode), then watcher generates and returns a new key.
# NOTE: we don't interpret this key, we just pass it to invocations of our tools

# Set up watcher directory, unless already set
$ENV{'LDV_WATCHER_SRV'} ||= "$config->{work_dir}/watcher";
vsay ('INFO',"Watcher server is $ENV{'LDV_WATCHER_SRV'}\n");

my ($key_str,undef) = LDV::Utils::watcher_cmd('key','dscv');
vsay ('DEBUG',"Got key string $key_str.\n");
my @watcher_key = split /,/,$key_str;

#====
# We should pack files for the parent task to process afterwards.  Use these functions
use File::Spec::Functions qw(rel2abs);
# Ref since it's called inside the closure in Twig parser
my $files_for_parent = {};
sub for_parent
{ local $_; for (@_){
	my $fname = rel2abs($_);
	vsay('TRACE',"Will package file: '$fname'\n");
	$files_for_parent->{$fname} = 1;
}}
sub not_for_parent
{ local $_; for (@_){
	my $fname = rel2abs($_);
	vsay('TRACE',"Will NOT package file: '$fname'\n");
	$files_for_parent->{$fname} = 0;
}}
sub files_for_parent
{ local $_; return (grep {$files_for_parent->{$_}} keys %$files_for_parent); }
sub for_parent_if_exists
{ local $_; for (@_){
	my $fname = rel2abs($_);
	if (-f $fname){
		vsay('TRACE',"Will package file: '$fname'\n");
		$files_for_parent->{$fname} = 1;
	}else{
		vsay('TRACE',"Will NOT package file: '$fname' (doesn't exist)\n");
	}
}}
#====

###########################
# EXCEPTION HANDLING!
eval {

sub kill_watcher
{
	vsay 20, "Shutting down watcher\n";
	# Report failure to the watcher (reckon we've registered a key for our process?)
	LDV::Utils::watcher_cmd('fail','dscv',@watcher_key);
	die $@;
}

$SIG{QUIT} = \&kill_watcher;
# Terminage signal is used for graceful termination on shutdown.  If a node in a cluster is shut down it's not an integration error.
$SIG{TERM} = \&kill_watcher unless $ENV{'LDV_IN_CLUSTER'};
# Interrupt signal (Ctrl+C) is used to control cluster outside of a cloud; do not make it an integration error.
$SIG{INT} = \&kill_watcher unless $ENV{'LDV_IN_CLUSTER'};


# Actual work.

# Create basic directory structure
my $dscv_base_dir = "$config->{work_dir}/dscv";
mkpath($dscv_base_dir);
my $dscv_tmp_dir = "$config->{work_dir}/tmp/dscv";
mkpath($dscv_tmp_dir);

$config->{report} ||= "$dscv_base_dir/report.xml";

# Prepare running environment
my $shared_dir = $config->{dscv_dir};
my $rcv_frontends_dir = "$shared_dir/rcv";

# Check how many times blast was run.  It's <number of rule-models>*<number of mains>
my $expected_blast_runs = 0;
# How many BLAST runs are expected after rule instrumentation (the discrepancy between these two probably denotes an error)
my $rule_instr_mains = 0;

# Send cmdfile to Instrumentor for each rule.

use DSCV::RCV::Utils;
use XML::Twig;

# For now we just pass the whole file to Instrumentor
# This is just a simple check that XML is correct, and to perform sanity checks
my $process_input = XML::Twig->new( twig_handlers => { cc => \&execute_cmd, ld=>\&execute_cmd, basedir=>\&get_driver_dir });

sub execute_cmd
{
	my ($twig, $cmdT) = @_;
	# Sanity check
	if ($cmdT->tag eq 'ld') {
		$expected_blast_runs += scalar $cmdT->children("main");
	}
}
my $driver_dir = undef;
sub get_driver_dir
{
	my $bdT = $_[1];
	$driver_dir = $bdT->text();
}
$process_input->parsefile($config->{cmdfile});

# Sanity check for blast runs
$expected_blast_runs *= scalar @{$config->{properties}};
vsay ('DEBUG',"Expecting $expected_blast_runs runs of RCV backend.\n");

# Now, if we don't expect RCV to be run, we just skip all the checking!  It takes considerable time: copying and building driver, running Rule Instrumentor, running RCV's just to get information that no runs was actually performed.
if ($expected_blast_runs == 0 && !$config->{force}){

	vsay 10,"Nothing will be run (no mains!).  Printing final report...\n";

	# Mark the resultant report as that for parent.
	for_parent($config->{report});

	my $big_reportT = XML::Twig::Elt->new("reports");

	# Walk through cmdfile and build the report according to it
	sub fake_report_about_command
	{
		my ($twig,$cmdT) = @_;
		# For this command, find all entities that refer to it and print them to outfile
		my $id = $cmdT->id;
		my $name = $cmdT->tag;

		# Let's create new command tag
		my $copyT = $cmdT->copy();
		# Replace 'id' with 'ref'
		$copyT->set_att('ref'=>$copyT->att('id'));
		$copyT->del_att('id');
		if ($name eq 'cc'){
			# Nice and shiny bogus OK report
			# This adds RI's info, not DSCV's (yes, I know about the name)
			add_dscv_info($copyT,'OK',"RI wasnt even run--no mains anyway",'rule-instrumentor');
			# This adds DSCV's
			add_dscv_info($copyT,'OK',"DSCV wasnt even run--no mains anyway");
		}elsif ($name eq 'ld'){
			# RI is not necessary for DSCV.  Moreover, it will make ldv-upload fail with trace validation error.
			# DSCV's failure report
			add_dscv_info($copyT,'FAILED','No entry points specified');
		}else {
			vsay ('TRACE',"Encountered tag $name, skipping...\n");
		}
		$copyT->paste(last_child=>$big_reportT);
	}
	XML::Twig->new( twig_roots=> {
		'cc'=>\&fake_report_about_command,
		'ld'=>\&fake_report_about_command,
	})->parsefile($config->{cmdfile});

	my $BIG_REPORT; open $BIG_REPORT, ">", $config->{report} or die;

        $big_reportT->set_pretty_print('indented');
        $big_reportT->print($BIG_REPORT);
	close $BIG_REPORT;

	vsay 20,"Final report is written to $config->{report}\n";

	# Leave
	goto FINALIZE;
}

# Function that copies driver
use File::Copy::Recursive qw(dircopy);
my $copy_driver = sub
{
	my $to = shift or die;
	defined $driver_dir or die;
	vsay('DEBUG',"Copying driver from '$driver_dir' to '$to'...\n");
	dircopy($driver_dir,$to) or die $!;
};

$" = ' ';	# For error reporting

# Prepare working dirs

my $out_cmd_dir = "$dscv_base_dir/cmdfiles";
mkpath ($out_cmd_dir);
my $for_rcv_dir = "$dscv_base_dir/rcv";
mkpath ($for_rcv_dir);

my $ri_basedir = "$dscv_base_dir/ri";
mkpath ($ri_basedir);

# Get list of properties to check
my @properties = @{$config->{properties}};

# For each rule model, record verifier used for that
my %verifier_for = ();


for my $property (@properties){
	# make outcmdfile
	my $outfile = "$out_cmd_dir/cmd$property.xml";
	# Prepare directory
	my $ri_basedir_for_rule;
	if ($mode ne 'inline'){
		$ri_basedir_for_rule = "$ri_basedir/$property";
		mkpath($ri_basedir_for_rule);
		# Copy driver
		$copy_driver->($ri_basedir_for_rule);
	}else{
		$ri_basedir_for_rule = './'
	}

	vsay ('DEBUG',"CMDFILE    : ".$config->{cmdfile}."\n");
	vsay ('DEBUG',"CMDFILE-OUT: ".$outfile."\n");
	vsay ('TRACE',"BASEDIR: ".$ri_basedir_for_rule."\n");
	my @rule_instr_args = (
        #	$config->{timestats},
	#      	"--pattern=.*,ALL",
	#      	"--output=".$config->{work_dir}."/rule-instrumentor/$property/stats.xml",
		$config->{rule_ins},
		"--basedir=$ri_basedir_for_rule",
		"--rule-model=$property",
		"--cmdfile=$config->{cmdfile}",
		"--cmdfile-out=$outfile",
	);
	push @rule_instr_args, "--cache=$ENV{'RI_CACHE'}" if $ENV{'RI_CACHE'};
	push @rule_instr_args, "--skip-norestrict" if $ENV{'RI_CACHE_MODE'} eq 'git';
	push @rule_instr_args, "--suppress-config" if $mode eq 'inline';
	vsay (10,"Calling rule_instrumenter for rule model $property...\n");
	vsay (20,@rule_instr_args,"\n");
	system @rule_instr_args and die "INTEGRATION ERROR.  Cmdline:  @rule_instr_args";
	#system "cp $config->{cmdfile} $outfile";

	# Call C verifier

	# In this loop, we should convey to RCV the following information:
	# - The ld command and its entry point to verify
	# - The list of cc commands (in the order they are in the original cmdstream) related to this ld command
	# - The task file with all the necessary information from the rule model database, and with the filenames to put the results to.

	my $verifier_dir = "$for_rcv_dir/$property";
	mkpath($verifier_dir);

	my $dir = $verifier_dir;
	# Mapping between the outfile for a CC command and its ID.  Grepping this will provide us with the list of CC command ids relevant to the LD command.  Note that as we need to preserve the ordering, we'll also keep the number of CC command.
	my $id_for_cc_outfile = {};
	my $cc_cmd_number = 0;
	# Gather registry of .c files in each cc command
	my $infiles_for_cc = {};
	# Get basedir (it is a reference--due to closure)
	my $basedir = undef;
	my $bdr = \$basedir;
	my $basedir_handler = sub {
		my (undef,$bdT) = @_;
		$$bdr = $bdT->text();
	};
	my $ccld_handler = sub{
		my ($twig, $cmdT) = @_;

		my $id = $cmdT->att('id');

		if ($cmdT->name eq 'cc'){
			# Add the info about this cmd to be found via <ld> inputfiles
			for my $outf ($cmdT->children_text('out')){
				$id_for_cc_outfile->{$outf} = [$cc_cmd_number++, $id];
			}
			# Add the info about the C files (will be used to send them to RCV node in distributed runs)
			for my $inf ($cmdT->children_text('in')){
				$infiles_for_cc->{$id} ||= [];
				push @{$infiles_for_cc->{$id}}, $inf;
			}
		}elsif ($cmdT->name eq 'ld'){
			# Now form the taskfile for each main in this LD command

			# For each <ld>/main pair there should be a separate name.  Since <ld>s differ by the file name, we'll use the file-inside-driver($target) as a discriminator.
			my $out_file = $cmdT->first_child_text('out');
			my $target = Utils::unbasedir_maker($basedir)->($out_file);

			# Alias for the easy portinf of the older code
			my $workdir = $verifier_dir;

			# Get entry points
			my @mains = $cmdT->children_text('main');
			@mains or vsay("WARNING", "No mains specified for file ".$out_file."\n");

			# We do not need to do anything else if there are no entry points. Note that this returns from the sub, and keeps checking the further <cc> and <ld> cmds.
			return unless @mains;

			# List of error locations
			my @errlocs = $cmdT->children_text('error');
			# Get hints tag
			my $hintsT = $cmdT->first_child('hints');
			# Report file (the placeholders here and below will be replaced with main names)
			my $in_main = "$workdir/main-%s";
			my $report = "$in_main/reports/$target.report";
			# Tool debug file (to dump the trace of the tool)
			# NOTE that the actual debug file name may be different if the tool's frontend chooses to run the trace through the archiver.  This is not a problem in cluster, as the decision of what to send to the parent node doesn't rely on this subroutine.
			my $debug = "$in_main/reports/$target.debug";
			# Trace file (to dump the error trace)
			my $trace = "$in_main/reports/$target.trace";
			# Time stats file
			my $timestats = "$in_main/reports/$target.timestats.xml";

			# Get all the cc commands relevant to this one
			my @ld_infiles = $cmdT->children_text('in');
			local $_;
			my @cc_cmds = map {$id_for_cc_outfile->{$_}} @ld_infiles;
			# Sort them in the order they occured in the stream, and strip the index
			@cc_cmds = sort { $a->[0] <=> $b->[0] } @cc_cmds;
			@cc_cmds = map {$_->[1]} @cc_cmds;


			# Determine what engine we use
			my $engine = $config->{verifier_override}->{rcv} || $cmdT->first_child_text('engine') or die "Command $id is without <engine> s pecification, and --rcv-verifier option is not set";

			# Save verifier information for error-trace-visualizer
			$verifier_for{$property} = $engine;

			# Common arguments for the verifier command
			my %common_vercmd_args = (
				cmd_id=>$cmdT->att('id'),
				hints=>$hintsT,
				mains=>\@mains,
				errlocs=>\@errlocs,
				report=>$report,
				trace=>$trace,
				debug=>$debug,
				dbg_target=>$target,
				workdir=>$in_main,
				timestats=>$timestats,
				cc_ids => \@cc_cmds,
				engine => $engine,
				cmdfile => $outfile,
			);

			# Now spawn an RCV for each main in this <ld>
			for my $main (@mains){
				# Put the main name instead of the placeholders
				my %args = DSCV::RCV::Utils::args_for_main($main, %common_vercmd_args);

				# Only send one main (this may be revamped in the future)
				$args{mains} = [$main];

				my $task_fname = sprintf "%s/task-for-%s",$workdir,$main;
				# Write the task to an xml file
				vsay('TRACE',"Writing command for a single RCV to '$task_fname'\n");
				my $TASK; open $TASK, ">", $task_fname or die "Can't open '$task_fname': $!";
				Utils::hash_to_xml(\%args,'input',$TASK);
				close $TASK;
				vsay('DEBUG',"Written RCV single command to '$task_fname'\n");

				# Spawn the RCV command
				vsay('INFO',"Spawning RCV with task $task_fname workdir $args{workdir} for ld $args{cmd_id}, main $main");
				$ENV{'RCV_FRONTEND_CMD'} = "$shared_dir/rcv-launcher";

				# CLUSTER OPERATIONS

				# Notify about the number of RCVs the DSCV is to expect
				$rule_instr_mains += 1;

				# The files to send:
				# - cmdfile
				# - Instrumented driver (and model) sources from $ri_basedir_for_rule (because some files from it, such as headers, are only indirectly referenced!  .c files are not enough).
				# - do NOT send .c files from <cc> commands, as they'll be sent in the prevopis point
				# - Rule-instrumentor's tempdir (autoconf is there, as well as some other files may be).  TODO check if it can be shrunk.

				LDV::Utils::watcher_cmd_noread('queue','rcv',$task_fname,$args{workdir},@watcher_key,'rcv',$property,$args{cmd_id},$main,'@@',$args{cmdfile}, $ri_basedir_for_rule, "$ENV{'WORK_DIR'}/rule-instrumentor/$property");
			}
		}
	};
	my $verify_Twig = XML::Twig->new( twig_handlers => { basedir=>$basedir_handler, cc => $ccld_handler, ld=>$ccld_handler });

	$verify_Twig->parsefile($outfile);
}

#=~~~~~~~======~~~~~~======~~~~~~======~~~~~~======
#              wait for all RCV tasks


# Wait for the number of RCVs we should spawn
my $rcvs_consumed = 0;

# Hash of the keys returned.  A key may arrive twice (if this node is a re-instantiation of a terminated node, and a child of that terminated node finishes its work after another instance of it was queried).
my $keys_arrived = {};

# Callback to process one result of an RCV.  It's called synchronously.
my $process_rcv_result = sub {
	my @rv = split /,/,$_[0];
	LDV::Utils::push_instrument('dscv');
	vsay('TRACE',"In callback: @rv\n");

	# In cluster mode such a call would just block forever...
	die "Tried to wait for more processes than we should!" unless @rv;

	my ($path,$contents,@key) = @rv;
	# Discard generic part of the key returned
	for my $wk (@watcher_key){
		my $k = shift @key;
		die "Assertion failed: '$k' == '$wk'" if $k ne $wk;
	}
	vsay('TRACE',"Done key loop \n");
	my (undef,$rule_id,undef,$cmd_id,$main_id) = @key;

	# Check if such key has already arrived
	my $key_id = join(".",@key);
	unless (exists $keys_arrived->{$key_id}){
		$keys_arrived->{$key_id} = 1;
		vsay('INFO',"Successfully waited for $rule_id, $cmd_id, $main_id\n");
		# unpack the incoming package
		vsay('DEBUG',"Unpacking '$contents' to '$path'\n");
		my ($files,undef) = LDV::Utils::watcher_cmd_readall('unpack',$path,$contents);
		local $_;
		for_parent($_) for @$files;

		LDV::Utils::push_instrument('dscv');
		# Return that we have waited for one key succesfully
		return 1;
	}else{
		# This is not the first time, just note that.
		vsay('WARNING',"Received result for $rule_id, $cmd_id, $main_id AGAIN, ignoring.\n");

		LDV::Utils::pop_instrument('dscv');
		# Notify the parent that we have read zero useful keys
		return 0;
	}
};

while ($rcvs_consumed < $rule_instr_mains) {
	my $more = $rule_instr_mains-(scalar keys %$keys_arrived);
	vsay('INFO',sprintf("Waiting for %d more RCVs...\n",$more));
	$rcvs_consumed += LDV::Utils::watcher_cmd_callback($more,$process_rcv_result,'wait','rcv',@watcher_key,'#');
}

vsay('INFO',sprintf("Results from %d RCVs are here.  Processing...\n",$rule_instr_mains));

# Ok, now all sub-commands are waited for, and their xml reports reside in our workdir

for my $rule_id (@properties){

	# After unpacking, we have the contents of XML report of RCV, traces, debug files and time stats on the local filesystem
	my $verifier_dir = "$for_rcv_dir/$rule_id";
	my $ri_basedir_for_rule = ($mode ne 'inline')? "$ri_basedir/$rule_id" : './';
	# Assignment to port the old code
	my $property = $rule_id;

	# Now we concatenate all reports into one file and pass that file to rule-instrumenter for it to tune that big report.
	my $before_ri_rep_name = "$verifier_dir/report.xml";
	my $after_ri_rep_name = "$verifier_dir/report_after_ri.xml";

	my $BIG_REPORT; open $BIG_REPORT, ">", $before_ri_rep_name or die "Can't open '$before_ri_rep_name' for writing: $!";

	my $bigRepT = XML::Twig::Elt->new("reports");

	my $cat_rep=sub{
		my ($report_file) = @_;
		vsay('DEBUG',"Chewing RCV report '$report_file'...\n");
		XML::Twig->new(twig_roots=> {
			'reports' => sub {
				my ($twig,$repT) = @_;
				local $_;
				$_->copy->paste($bigRepT) for $repT->children;
			},
		})->parsefile($report_file);
		not_for_parent($report_file)
	};
	DSCV::RCV::Utils::foreach_report($verifier_dir,$cat_rep);

	$bigRepT->set_pretty_print('indented');
	$bigRepT->print($BIG_REPORT);
	close ($BIG_REPORT);

	# Now feed that BIG_REPORT to rule-instrumenter
	my @rule_instr_report = ($config->{rule_ins},
		"--basedir=$ri_basedir_for_rule",
		"--rule-model=$property",
		"--report=$before_ri_rep_name",
		"--report-out=$after_ri_rep_name"
	);
	vsay ('NORMAL',"Calling rule-instrumenter to fix up report for rule $property...\n");
	vsay (20, "@rule_instr_report\n");
	system @rule_instr_report and die "INTEGRATION ERROR.  Cmdline:  @rule_instr_report";
	for_parent($after_ri_rep_name);
}

# Now we concatenate all reports after Rule instrumentor invocations into one file

# Load information from all RI reports into one XML tree (in memory)
my $reportsT = XML::Twig::Elt->new("reports");

for my $property (@properties){
	my $verifier_dir = "$for_rcv_dir/$property";
	my $after_ri_rep_name = "$verifier_dir/report_after_ri.xml";

	XML::Twig->new(
		twig_roots=> {'reports' => sub {
			my ($twig,$rule_repT) = @_;
			local $_;
			$_->copy->paste($reportsT) for $rule_repT->children;
		}}
	)->parsefile($after_ri_rep_name);

	not_for_parent($after_ri_rep_name);
}
# Mark the resultant report as that for parent.
for_parent($config->{report});

# Add <dscv> tag to the report
sub add_dscv_info
{
	my ($rep_entityT,$status_str,$descr,$name) = @_;

	my %timestats;
	my $rcv_tag = $rep_entityT->first_child('rcv');
	if ($rcv_tag) {
		foreach my $rcv_time ($rcv_tag->children('time')) {
			$timestats{$rcv_time->att('name')} = $rcv_time->text;
		}
	} 
	my $ri_tag = $rep_entityT->first_child('rule-instrumentor');
	if ($ri_tag) {
		foreach my $ri_time ($ri_tag->children('time')) {
			$timestats{$ri_time->att('name')} += $ri_time->text;
		}
	} 


	$name ||= 'dscv';
	$descr ||= '';

	# calculate dscv time as ALL=rcv ALL time + ri ALL time for this tag
	my $dscvT = XML::Twig::Elt->new($name);
	XML::Twig::Elt->new('status',{},$status_str)->paste(last_child=>$dscvT);
	#XML::Twig::Elt->new('time',{'note'=>'always zero for now :-('},'0')->paste(last_child=>$dscvT);
        foreach(keys %timestats) {
		/ALL/ or next;
                my $time_elt = XML::Twig::Elt->new('time',$timestats{$_});
                $time_elt->set_att( name =>$_);
                $time_elt->paste(last_child=>$dscvT);
        } 
	XML::Twig::Elt->new('desc',{},$descr)->paste(last_child=>$dscvT);
	$dscvT->paste(last_child=>$rep_entityT);
}

my $big_reportT = XML::Twig::Elt->new("reports");

# Walk through cmdfile and build the report according to it
sub report_about_command
{
	my ($twig,$cmdT) = @_;
	# For this command, find all entities that refer to it and print them to outfile
	my $id = $cmdT->id;
	my $name = $cmdT->tag;

	my @reports = $reportsT->findnodes("$name\[\@ref=\"$id\"\]");

	if (@reports){
		for my $repT_ (@reports){
			my $repT = $repT_->copy();
			add_dscv_info($repT,'OK');
			$repT->paste(last_child=>$big_reportT);
		}
	}else{
		my $repT = XML::Twig::Elt->new($name,{'ref'=>$id});
		# Try to find out what happened
		# Was it because that main hasn't been specified?
		unless ($reportsT->findnodes("$name\[\@ref=\"$id\"\]/main")){
			add_dscv_info($repT,'FAILED','No entry points specified');
		}else{
			# If we don't know the reason, let's leave the field empty
			add_dscv_info($repT,'FAILED');
		}
		$repT->paste(last_child=>$big_reportT);
	}
}
XML::Twig->new( twig_roots=> {
	'cc'=>\&report_about_command,
	'ld'=>\&report_about_command,
})->parsefile($config->{cmdfile});

my $BIG_REPORT; open $BIG_REPORT, ">", $config->{report} or die;
$big_reportT->set_pretty_print('indented');
$big_reportT->print($BIG_REPORT);
close $BIG_REPORT;

my $report_verbosity = ($mode ne 'inline') ? 'INFO' : 'NORMAL';
vsay $report_verbosity,"Final report is written to $config->{report}\n";

1;
} or do {
	vsay 4, "Exception occured: $@" if $@;
	vsay 4, "Fatal error.  Stopping services before reporting...\n";
	# FIXME: we shouldn't check for cluster here, but as of today it's more important to not send fails on signals
	kill_watcher() unless $ENV{'LDV_IN_CLUSTER'};
	# We die in kill_watcher, no need to exit here... but just in case...
	exit 1;
};

FINALIZE:

# Report success to the watcher
LDV::Utils::watcher_cmd('success','dscv',@watcher_key,'@@',files_for_parent());

# EXCEPTION HANDLING!
###########################

=head1 DOCUMENTATION FOR DEVELOPERS

=cut

=head1 AUTHORS

The DSCV component was implemented by

Pavel Shved L<shved@ispras.ru>, 2010

ISP RAS L<http://ispras.ru/>, 2010


