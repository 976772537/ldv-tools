#!/usr/bin/perl

# Fix reports by adding tag and driver information to them
sub usage
{ die<<EOF
Usage:
	report-fixup report.xml git_tag driver_name driver_origin sources_dir work_dir
EOF
}

my ($report_xml,$git_tag,$driver_name,$driver_origin,$sources_dir,$work_dir) = @ARGV;
$work_dir or usage();

use strict;
use XML::Twig;

sub current_date
{
	my ($name) = @_;
	my ($sec,$min,$hour,$mday,$mon,$year,$wday,$yday,$isdst) = localtime(time);
	$year+=1900; $mon+=1;	#required due to date-time format printed by localtime.  Months usually start with 1.
	my @vals = qw(sec min hour mday mon year wday yday isdst);
	my $elt_result = XML::Twig::Elt->new($name);
	# Print variable values into tags that correspond to their names
	local $_;
	XML::Twig::Elt->new($_,{},eval "\$$_")->paste($elt_result) for @vals;

	return $elt_result;
}

sub handle_report
{
	my ($twig, $cmdT) = @_;
	my $launchT = XML::Twig::Elt->new('launch_info');
	my $verifier = undef;
	# Add more verifiers here if you want to override them
	$verifier ||= $ENV{'RCV_VERIFIER'};
	XML::Twig::Elt->new('globalverifier',{},$verifier)->paste($launchT) if defined $verifier;
	my $taskT = XML::Twig::Elt->new('task');
	if ($ENV{'LDV_TASK_ID'}){
		XML::Twig::Elt->new('task_id',{},$ENV{'LDV_TASK_ID'})->paste($taskT);
	}else{
		if ($ENV{'LDV_TASK_NAME'}){
			XML::Twig::Elt->new('task_name',{},$ENV{'LDV_TASK_NAME'})->paste($taskT);
		}
		current_date('timestamp')->paste($taskT);
		my $user = `id -un`; chomp $user;
		XML::Twig::Elt->new('username',{},$user)->paste($taskT);
		if ($ENV{'LDV_TASK_DESCRIPTION'}) {
			XML::Twig::Elt->new('description',{},$ENV{'LDV_TASK_DESCRIPTION'})->paste($taskT);
		}
		XML::Twig::Elt->new('driver',{},$driver_name)->paste($taskT);
		XML::Twig::Elt->new('driver_origin',{},$driver_origin)->paste($taskT);
	}
	$taskT->paste($launchT);
	XML::Twig::Elt->new('driver',{},$driver_name)->paste($launchT);
	XML::Twig::Elt->new('driver_origin',{},$driver_origin)->paste($launchT);
	XML::Twig::Elt->new('tag',{},$git_tag)->paste($launchT);
	XML::Twig::Elt->new('sources',{},$sources_dir)->paste($launchT) if defined $sources_dir && -d $sources_dir;
	XML::Twig::Elt->new('workdir',{},$work_dir)->paste($launchT) if defined $work_dir && -d $work_dir;
	$launchT->paste($cmdT);
	$cmdT->set_pretty_print('indented');
	$cmdT->print();
}

use Cwd 'abs_path';
$work_dir = abs_path($work_dir);
# Replace trace path with path relative to $work_dir.  Work_dir is saved in launch_info tag.
sub fix_trace
{
	my ($twig, $traceT) = @_;
	my $trace_absolute = $traceT->text;
	return unless $trace_absolute;
	$trace_absolute = abs_path($trace_absolute);
	$trace_absolute =~ s/^$work_dir\/*//;
	$traceT->set_text($trace_absolute);
}

XML::Twig->new( twig_roots => { reports => \&handle_report, trace => \&fix_trace }, twig_print_outside_roots => \*STDOUT )->parsefile($report_xml);


